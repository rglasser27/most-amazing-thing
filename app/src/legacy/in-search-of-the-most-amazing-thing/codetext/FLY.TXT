0REM fly
1 DIM CH(3):GOSUB 25500:KEY(1) ON:ON KEY(1) GOSUB 25450
2 DEF FNS(P,L)=VAL(MID$(B$,P,L))
3 DEF FNR(X)=INT(RND(1)*X+1)
4 DIM HUT%(10):FOR Z2=1 TO 10:READ HUT%(Z2):NEXT:DATA 227,161,173,189,147,127,143,149,151,187
5 DIM WIND$(3),A1%(12),BLINE%(35),CURSOR%(16),BLOCK%(106),SON%(174),SOFF%(174)
8 GOSUB 20040:IF BRANCH=3 THEN B$="CARINT.CRN":GOSUB 25280:GOTO 8000
9 GOTO 2000
10 REM                                                                                 keyboard scan
11 A$=INKEY$:TI=TI+1:Z8=TI MOD 10:IF Z8>0 THEN 14
12 DEF SEG=&H40:POKE &H17,&H40:DEF SEG:SLEEP=SLEEP+.1:HUNGRY=HUNGRY+.2:IF SLEEP>300 AND FNR(AN)=1 THEN LOCATE 1,10:PRINT"        sleepy?       ":FOR Z9=1 TO SLEEP:SOUND 144,.0001:NEXT:SOUND 144,0:LOCATE 1,18:PRINT"       ":AN=AN-1:IF AN<1 THEN AN=1
13 IF TI>32000 THEN TI=0
14 RETURN
1000 IF BURNER AND FUEL>0 THEN VOLUME=VOLUME+.1:FUEL=FUEL-.05
1010 VOLUME=VOLUME-.01
1020 IF VENT THEN VOLUME=VOLUME-.2
1030 IF VOLUME>100 THEN VOLUME=100
1040 IF VOLUME<20 THEN VOLUME=20
1060 DISP=VOLUME*((-4/5000)*ALT+10)
1070 IF DISP>WEIGHT THEN ALT=ALT+(DISP-WEIGHT)/2:GOTO 1100
1080 ALT=ALT+(DISP-WEIGHT)*4
1090 IF ALT<0 THEN ALT=0
1100 IF FUEL<0 THEN FUEL=0
1110 IF Z8>0 THEN RETURN
1115 DUMMY=FRE("")
1116 IF TI>32000 THEN TI=0
1120 A=ALT\500
1130 DIR=36*A+INT(TI/100)
1140 DIR=DIR MOD 360
1150 VEL=3*A-12:IF ALT<2000 THEN VEL=0
1165 Z7=TI MOD 8000
1166 IF Z7=200 THEN HIGH=INT(RND*4)
1170 IF Z7<200 AND ALT>100 THEN VEL= 125+SIN(TI/50)*25:DIR=HIGH*90
1180 Q1=(DIR*6.283186)/360:XCOORD=XCOORD+(VEL/Z0)*SIN(Q1):YCOORD=YCOORD+(VEL/Z0)*COS(Q1)
1190 IF XCOORD<0 THEN XCOORD=0
1200 IF YCOORD<0 THEN YCOORD=0
1210 IF XCOORD>5249 THEN XCOORD=5249
1220 IF YCOORD>5249 THEN YCOORD=5249
1230 STATE=(YCOORD\350)*15+XCOORD\350
1280 RETURN
2000 CLS
2040 PSET(32,8):DRAW "r256 d172 l256 u172":PSET(88,69):DRAW "r45 u16 r54 d16 r45 d96 l144 u96"
2050 FOR Y1=75 TO 147 STEP 24:PSET(37,Y1):DRAW"r46 d16 l46 u16":NEXT
2060 FOR Y1=75 TO 147 STEP 24:PSET(237,Y1):DRAW"r46 d16 l46 u16":NEXT
2090 PAINT STEP(1,1),3,3:GET (238,148)-(282,162),BLOCK%:PUT(238,148),BLOCK%:GET(1,1)-STEP(142,0),BLINE%
2120 LOCATE 11,6:PRINT "DIR-?";:LOCATE 14,6:PRINT "High?";:LOCATE 17,6:PRINT "Home1";:LOCATE 20,6:PRINT"Storm";:LOCATE 11,31: PRINT"Fuelo";:LOCATE 14,31:PRINT"AutoP";:LOCATE 17,31:PRINT"Radar";:LOCATE 20,31:PRINT"Hut 1";
2130 PSET(56,27):DRAW"l6 d16 r56 u16 l6":PSET(62,24):DRAW"d10 r30 u10 l30":PSET(52,29):DRAW"d12 r52 u12":LINE(62,24)-(52,29):LINE(62,34)-(52,41):LINE(92,24)-(102,29):LINE(92,34)-(102,41):DRAW"bm55,28l4d14r54u14l4":PAINT(63,25),3,3
2131 GET(50,24)-(106,46),SON%
2140 PSET(139,43):DRAW"l6 u16 r56 d16 l6":PSET(145,36):DRAW"d10 r30 u10 l30":PSET(135,41):DRAW"u12 r52 d12":LINE(135,29)-(145,36):LINE(135,41)-(145,46):LINE(185,29)-(175,36):LINE(185,41)-(175,46):GET(133,24)-(189,46),SOFF%
2150 PUT (50,24),SON%:PUT(50,24),SOFF%:PUT(216,24),SOFF%
2151 IF BURNER THEN Z6=50:GOSUB 2350
2152 IF VENT THEN Z6=133:GOSUB 2350:Z0=100
2153 IF MAP THEN Z6=216:GOSUB 2350
2160 LOCATE 3,8:PRINT"BURNER     DROP      WHERE";
2170 X1=60:Y1=55:X2=96:Y2=24:X3=60:Y3=31:X4=252:Y4=151:X5=38:Y5=76:Z1=1
2180 PSET(X1,Y1):DRAW "r8 d9 l8 u9":PAINT STEP(1,1),3,3:GET(X1,Y1)-(X1+8,Y1+9),CURSOR%
2185 PUT(X5,Y5),BLOCK%
2190 KEY OFF:LOCATE 25,17:PRINT"L=LEAVE";:GOSUB 3510
2200 GOSUB 11
2210 IF A$="" THEN 2270
2220 IF A$=" " AND (Y1=79 OR Y1=103 OR Y1=127 OR Y1=151) AND (X1<90 OR X1>228) THEN GOSUB 2370:GOTO 2270
2230 IF A$=" " AND Y1=31 THEN GOSUB 2300:GOTO 2270
2250 IF ASC(A$)=0 THEN GOSUB 3440
2260 IF A$="l" OR A$="L" THEN 8000
2270 GOSUB 1000
2271 IF Z8>0 THEN 2280
2272 IF MAP=0 THEN 2275
2273 IF XCOORD<1750 OR XCOORD>3500 OR YCOORD<1750 OR YCOORD>3500 THEN LOCATE 8,18:PRINT"otbrak";:GOTO 2275
2274 LOCATE 8,18:PRINT " ";CHR$(40+INT(XCOORD/70));" ";:PRINT USING "##";INT(YCOORD/70-24);:PRINT" "
2275 IF FOREC=1 AND (Z7<200 OR Z7>7700) AND Z1<>4 THEN PUT(38,148),BLOCK%:SOUND 400,2:PUT(38,148),BLOCK%
2280 ABORT=0:ON Z1 GOSUB 3570,2540,2610,2749,2830,2929,3030,3290
2285 ON ABORT GOTO 2220,2260
2290 GOTO 2200
2300 IF X1>60 THEN 2320
2310 Z6=50:GOSUB 2350:IF BURNER=1 THEN BURNER=0:RETURN
2314 IF VENT=1 THEN Z6=133:GOSUB 2350:VENT=0:Z0=9
2315 BURNER=1:RETURN
2320 IF X1>156 THEN 2340
2330 Z6=133:GOSUB 2350:IF VENT=1 THEN VENT=0:Z0=9:RETURN
2334 IF BURNER=1 THEN Z6=50:GOSUB 2350:BURNER=0
2335 VENT=1:Z0=100:RETURN
2340 Z6=216:GOSUB 2350:IF MAP=1 THEN MAP=0:LOCATE 8,18:PRINT"      ":RETURN
2345 MAP=1:RETURN
2346 RETURN
2350 PUT(Z6,24),SON%:PUT(Z6,24),SOFF%:RETURN
2360 RETURN
2370 REM
2372 PUT(X5,Y5),BLOCK%:IF Z1=6 THEN GOSUB 2945:GOSUB 3500:LOCATE 13,13:PRINT"Autopilot off":FOR X=1 TO 500:NEXT:Z0=9
2373 IF Y5=Y1-3 AND ABS(X1-X5)<50 THEN Z1=1:X5=38:Y5=76:GOTO 2390
2380 Y5=Y1-3:IF X1<90 THEN X5=38:Z1=(Y1-55)/24 ELSE X5=238:Z1=(Y1+41)/24
2390 PUT(X5,Y5),BLOCK%
2400 GOSUB 3500:ON Z1 GOSUB 3510,2480,2580,2750,2790,2870,3070,3260:RETURN
2480 IF ALTIM=0 THEN LOCATE 15,17:PRINT "High?":LOCATE 17,15:PRINT"not purchased":RETURN
2490 FOR Z2=156 TO 806 STEP 63:Z3=Z2/100: X6=163-32*COS(Z3):Y6=116-32*SIN(Z3):PSET(X6,Y6):NEXT
2500 LOCATE 10,21:PRINT"0";:LOCATE 11,24:PRINT"1";:LOCATE 13,26:PRINT"2";:LOCATE 17,26:PRINT"3";:LOCATE 19,24:PRINT"4";:LOCATE 20,21:PRINT"5";:LOCATE 19,18:PRINT"6";:LOCATE 17,16:PRINT "7";
2510 LOCATE 13,16:PRINT"8";:LOCATE 11,18:PRINT"9";
2520 Z2=ALT*6.283186:Z3=Z2/1000:Z4=Z3/10:Y6=-29*COS(Z3):Y8=-16*COS(Z4):X6=29*SIN(Z3):X8=16*SIN(Z4)
2530 PSET(163,116):LINE - STEP (X6,Y6):PSET(163,116):LINE - STEP (X8,Y8):X7=X6:X9=X8:Y7=Y6:Y9=Y8
2540 IF ALTIM=0 THEN RETURN
2550 IF Z2=ALT*6.283186 THEN RETURN
2560 Z2=ALT*6.283186:Z3=Z2/1000:Z4=Z3/10:Y6=-29*COS(Z3):Y8=-16*COS(Z4):X6=29*SIN(Z3):X8=16*SIN(Z4)
2570 PSET(163,116):LINE - STEP (X7,Y7),0:PSET(163,116):LINE - STEP (X9,Y9),0:PSET(163,116):LINE - STEP (X6,Y6):PSET(163,116):LINE - STEP (X8,Y8):X7=X6:X9=X8:Y7=Y6:Y9=Y8:RETURN
2580 IF HOMER=0 THEN LOCATE 15,17:PRINT "Home1":LOCATE 17,15:PRINT"not purchased":RETURN
2590 FOR Z2=1560 TO 8060 STEP 394:Z3=Z2/1000: X6=192-30*COS(Z3):Y6=122-30*SIN(Z3):PSET(X6,Y6):NEXT
2600 LOCATE 11,17:PRINT"HOME:":LOCATE 17,14:PRINT"miles":GOTO 2640
2610 IF HOMER=0 THEN RETURN
2620 IF TI MOD 20>0 THEN RETURN
2630 LOCATE 15,14:PRINT"    ":IF CL>0 THEN PSET(192,122):LINE-STEP(27*CLX/CL,-27*CLY/CL),0
2640 CLX=X0-XCOORD:CLY=Y0-YCOORD:CL=SQR(CLX^2+CLY^2)
2650 LOCATE 15,14:PRINT USING "####";CL:IF CL>0 THEN PSET(192,122):LINE-STEP(27*CLX/CL,-27*CLY/CL)
2660 RETURN
2749 IF TI MOD 20 <> 0 THEN RETURN
2750 IF FOREC=0 THEN LOCATE 15,17:PRINT "Storm":LOCATE 17,15:PRINT"not purchased":RETURN
2760 IF Z7<200 THEN LOCATE 13,13:PRINT"Storm in progress":LOCATE 15,13:PRINT "Winds from the":LOCATE 16,13:PRINT WIND$(HIGH);" up to 150":LOCATE 17,13:PRINT"mph.            ":RETURN
2770 IF Z7>6000 THEN LOCATE 13,13:PRINT"Storm warning:":LOCATE 15,13:PRINT "Winds from the":LOCATE 16,13:PRINT WIND$(HIGH);" up to 150":LOCATE 17,13:PRINT"mph in";(8000-Z7)\120;"units ":RETURN
2780 LOCATE 13,13:PRINT"                 ":LOCATE 15,13:PRINT"              ":LOCATE 16,13:PRINT"               ":LOCATE 17,13:PRINT"               ":RETURN
2790 IF FUELM=0 THEN LOCATE 15,17:PRINT "Fuelo":LOCATE 17,15:PRINT"not purchased":RETURN
2800 FOR Z2=100 TO 200 STEP 10:LINE(Z2,135)-STEP(0,5):NEXT:FOR Z2=100 TO 100+FUEL:LINE(Z2,100)-STEP(0,32):NEXT
2810 LOCATE 19,13:PRINT"E    1/2     F";
2820 RETURN
2830 IF FUELM=0 THEN RETURN
2840 IF Z2<100+FUEL THEN FOR Z2=Z2 TO 100+FUEL:LINE(Z2,100)-STEP(0,32):NEXT:Z2=Z2-1
2850 IF Z2>100+FUEL THEN FOR Z2=Z2 TO 100+FUEL STEP -1:LINE(Z2,100)-STEP(0,32),0:NEXT:Z2=Z2+1
2855 IF FUEL=0 THEN LOCATE 15,18:PRINT"EMPTY"
2860 RETURN
2870 IF PHANT=0 THEN LOCATE 15,16:PRINT "AutoP":LOCATE 17,15:PRINT"not purchased":RETURN
2880 TARGET=0:LOCATE 15,13:PRINT"Autopilot engaged":IF XCOORD=X0 AND YCOORD=Y0 THEN TARGET=1:RETURN
2885 LOCATE 17,13:PRINT"Setting course":LOCATE 18,13:PRINT"for home"
2890 IF Y0=YCOORD THEN Z4=90:GOTO 2910
2891 IF X0<>XCOORD THEN 2900
2892 Z4=0:IF Y0<YCOORD THEN Z4=180
2893 GOTO 2915
2900 Z4=(180/3.141593)*ATN((Y0-YCOORD)/(XCOORD-X0))+90
2910 IF X0<XCOORD THEN Z4=Z4+180
2915 Z2=INT((500/36)*(Z4-INT(TI/100))):IF Z2<3000 THEN Z2=Z2+5000
2916 IF Z2<4000 AND ALT>6000 THEN Z2=Z2+5000
2920 RETURN
2929 IF PHANT=0 THEN RETURN
2930 IF XCOORD>X0-5 AND XCOORD<X0+5 AND YCOORD>Y0-5 AND YCOORD<Y0+5 AND ALT>2000 THEN TARGET=1
2931 IF TARGET=0 THEN 2935
2932 XCOORD=X0:YCOORD=Y0
2933 IF ALT>0 THEN GOSUB 2970:LOCATE 17,13:PRINT"    landing   ":LOCATE 18,13:PRINT"        ":RETURN
2934 GOSUB 2945:LOCATE 17,17:PRINT"arrived":RETURN
2935 IF TI MOD 20 = 0 THEN GOSUB 2890
2940 IF ABS(ALT-Z2)>100 THEN 2960
2945 IF BURNER THEN Z6=50:GOSUB 2350:BURNER=0
2950 IF VENT THEN Z6=133:GOSUB 2350:VENT=0:Z0=6
2955 RETURN
2960 IF ALT<Z2 THEN 3000
2970 IF BURNER THEN Z6=50:GOSUB 2350:BURNER=0
2980 IF VENT=0 THEN Z6=133:GOSUB 2350:VENT=1:Z0=150
2990 RETURN
3000 IF VENT THEN Z6=133:GOSUB 2350:VENT=0:Z0=6
3010 IF BURNER=0 THEN Z6=50:GOSUB 2350:BURNER=1
3020 RETURN
3030 IF RADAR=0 THEN RETURN
3040 IF TI MOD 30 <> 0 THEN RETURN
3050 FOR Z2=0 TO IN-2 STEP 2:PRESET(A1%(Z2+2),A1%(Z2+1)):NEXT:IF IN=0 THEN LOCATE 18,13:PRINT"                "
3060 REM
3070 IF RADAR=0 THEN LOCATE 15,18:PRINT "Radar":LOCATE 17,15:PRINT"not purchased":RETURN
3080 PSET(160,117):IN=0
3090 FOR Z5=STATE-16 TO STATE-14:IF (Z5\5 MOD 3)=1 AND Z5\75=1 THEN GOSUB 3170
3095 IF ABORT>0 THEN SOUND 50,0:RETURN
3100 NEXT
3110 FOR Z5=STATE-1 TO STATE+1:IF (Z5\5 MOD 3)=1 AND Z5\75=1 THEN GOSUB 3170
3115 IF ABORT>0 THEN SOUND 50,0:RETURN
3120 NEXT
3130 FOR Z5=STATE+14 TO STATE+16:IF (Z5\5 MOD 3)=1 AND Z5\75=1 THEN GOSUB 3170
3135 IF ABORT>0 THEN SOUND 50,0:RETURN
3140 NEXT:SOUND 50,0:LOCATE 18,17:PRINT"        ":IF IN=0 THEN LOCATE 18,13:PRINT "NO HUTS IN RANGE":SOUND 50,5:GOTO 3160
3150 SOUND 800,1
3160 FOR Z2=0 TO IN-2 STEP 2:PSET(A1%(Z2+2),A1%(Z2+1)):NEXT:Z3=TI:RETURN
3170 REM
3180 Z3=1
3190 FOR Z2=1 TO 5:X8=((Z5*HUT%(Z2))MOD 350)+(Z5 MOD 15)*350-XCOORD:Y8=((Z5*HUT%(Z2+5))MOD 350)+((Z5\15)*350)-YCOORD:X6=INT(X8*Z3+160):Y6=INT(117-Y8*Z3):IF X6>88 AND X6<232 AND Y6>69 AND Y6<165 THEN GOSUB 3210
3191 A$=INKEY$:IF A$="" THEN 3200
3192 IF A$=" " AND (Y1=79 OR Y1=103 OR Y1=127 OR Y1=151) AND (X1<90 OR X1>228) THEN ABORT=1:RETURN
3193 IF A$=" " AND Y1=31 THEN GOSUB 2300:GOTO 3200
3194 IF ASC(A$)=0 THEN GOSUB 3440
3195 IF A$="l" OR A$="L" THEN ABORT=2:RETURN
3200 NEXT:RETURN
3210 IN=IN+2:A1%(IN)=X6:IF A1%(IN)<89 THEN A1%(IN)=89
3220 A1%(IN-1)=Y6:IF A1%(IN-1)<70 THEN A1%(IN-1)=70
3250 RETURN
3260 IF RANGE=0 THEN LOCATE 15,16:PRINT "Hut 1":LOCATE 17,15:PRINT"not purchased":RETURN
3270 FOR Z2=1560 TO 8060 STEP 394:Z3=Z2/1000: X6=192-30*COS(Z3):Y6=122-30*SIN(Z3):PSET(X6,Y6):NEXT
3280 LOCATE 11,14:PRINT"NEAREST HUT:":LOCATE 17,14:PRINT"miles":GOTO 3320
3290 IF RANGE=0 THEN RETURN
3300 IF TI MOD 20>0 THEN RETURN
3310 LOCATE 15,14:PRINT"    ":IF CL>0 AND CL<20000 THEN PSET(192,122):LINE-STEP(27*CLX/CL,-27*CLY/CL),0
3320 CL=20000:CLX=20000:CLY=20000
3330 FOR Z5=STATE-16 TO STATE-14:IF (Z5\5)MOD 3=1 AND Z5\75=1 THEN GOSUB 3400
3335 IF ABORT>0 THEN RETURN
3340 NEXT
3350 FOR Z5=STATE-1 TO STATE+1:IF (Z5\5)MOD 3=1 AND Z5\75=1 THEN GOSUB 3400
3355 IF ABORT>0 THEN RETURN
3360 NEXT
3370 FOR Z5=STATE+14 TO STATE+16:IF (Z5\5)MOD 3=1 AND Z5\75=1 THEN GOSUB 3400
3375 IF ABORT>0 THEN RETURN
3380 NEXT:IF CL<20000 THEN LOCATE 15,14:PRINT USING "####";CL:IF CL>0 THEN PSET(192,122):LINE-STEP(27*CLX/CL,-27*CLY/CL)
3390 RETURN
3400 FOR Z2=1 TO 5:X8=((Z5*HUT%(Z2))MOD 350)+(Z5 MOD 15)*350-XCOORD:Y8=((Z5*HUT%(Z2+5))MOD 350)+((Z5\15)*350)-YCOORD:IF ABS(X8)>ABS(CLX) AND ABS(Y8)>ABS(CLY) THEN 3430
3410 IF X8=0 AND Y8=0 THEN CLX=0:CLY=0:CL=0:GOTO 3430
3420 Z6=SQR(X8^2+Y8^2):IF Z6<CL THEN CL=Z6:CLX=X8:CLY=Y8
3430 A$=INKEY$:IF A$="" THEN 3439
3431 IF A$=" " AND (Y1=79 OR Y1=103 OR Y1=127 OR Y1=151) AND (X1<90 OR X1>228) THEN ABORT=1:RETURN
3432 IF A$=" " AND Y1=31 THEN GOSUB 2300:GOTO 3439
3433 IF ASC(A$)=0 THEN GOSUB 3440
3434 IF A$="l" OR A$="L" THEN ABORT=2:RETURN
3439 NEXT:RETURN
3440 PUT (X1,Y1),CURSOR%:B$=RIGHT$(A$,1)
3450 IF B$=CHR$(72) THEN Y1=Y1-Y2:IF Y1<Y3 THEN Y1=Y3
3460 IF B$=CHR$(80) THEN Y1=Y1+Y2:IF Y1>Y4 THEN Y1=Y4
3470 IF B$=CHR$(75) THEN X1=X1-X2:IF X1<X3 THEN X1=X3
3480 IF B$=CHR$(77) THEN X1=X1+X2:IF X1>X4 THEN X1=X4
3490 PUT (X1,Y1),CURSOR%:RETURN
3500 FOR Z2=70 TO 164:PUT(89,Z2),BLINE%,PSET:NEXT:RETURN
3510 FOR Z2=1560 TO 8060 STEP 394:Z3=Z2/1000: X6=139-32*COS(Z3):Y6=116-32*SIN(Z3):PSET(X6,Y6):NEXT
3530 Y7=-29*COS(Z3):X7=29*SIN(Z3)
3540 LOCATE 10,18:PRINT"N";:LOCATE 15,23:PRINT"E";:LOCATE 20,18:PRINT "S";:LOCATE 15,13:PRINT"W";:LOCATE 16,25:PRINT"Your";:LOCATE 17,24:PRINT"Speed";:LOCATE 20,25:PRINT"mph";
3550 DIR1=DIR
3560 RETURN
3570 Z3 = ABS(DIR-DIR1):IF ABS(Z3)<180 THEN GOTO 3610
3590 IF DIR<DIR1 THEN DIR1=DIR1+(360-Z3)/3:GOTO 3630
3600 DIR1=DIR1-(360-Z3)/3:GOTO 3630
3610 IF DIR<DIR1 THEN DIR1=DIR1-Z3/3:GOTO 3630
3620 DIR1=DIR1+Z3/3
3630 IF DIR1<0 THEN DIR1=DIR1+360
3640 DIR1=DIR1 MOD 360
3650 Z2=DIR1*6.283186:Z3=Z2/360:Y6=-29*COS(Z3):X6=29*SIN(Z3):PSET(139,116):LINE - STEP (X7,Y7),0:IF VEL>0 THEN PSET(139,116):LINE - STEP (X6,Y6):X7=X6:Y7=Y6
3660 LOCATE 19,25:IF VEL=0 THEN PRINT "  O":RETURN
3670 PRINT USING "###";SIN(TI/10)+VEL;:RETURN
8000 REM
8006 SCREEN 1:GOSUB 25260
8090 LROW=25:LCOL=6:LOCATE LROW,LCOL:PRINT"Hit the SPACEBAR to move";
8095 AX=159:AY=174:GOSUB 8900
8100 GOSUB 11:GOSUB 1000:IF A$="" THEN 8100 ELSE IF A$<>CHR$(13) THEN 8110
8101 SOUND 800,2:IF HUNGRY>500 THEN FOR X=500 TO HUNGRY STEP 100:LOCATE 1,10:PRINT"        hungry?   ":FOR Y=1 TO 100:SOUND 144,.0001:NEXT:SOUND 144,0:NEXT
8102 ON CHOICE GOSUB 8215,8225,8235,8245,8255
8110 IF CHOICE=5 THEN CHOICE=1 ELSE CHOICE=CHOICE+1
8120 ON CHOICE GOSUB 8210,8220,8234,8240,8250: GOTO 8100
8210 GOSUB 8910:AX=106:AY=86:GOSUB 8900:LOCATE LROW,LCOL:PRINT"Hit RETURN to use the B-computer";:RETURN
8215 REM                                                                                     computer
8216 GOSUB 25550:RUN"bcomp.bas
8220 GOSUB 8910:AX=72:AY=31:GOSUB 8900:LOCATE LROW,LCOL:PRINT"Hit RETURN to sleep              ";:RETURN
8225 REM                                                                                     sleep
8226 CLS:LOCATE 15,15:PRINT"sleeping...":Z0=2:FOR SLEEP=SLEEP TO 1 STEP -1:TI=TI+1:Z8=TI MOD 10:GOSUB 1000:IF SLEEP MOD 400 = 99 THEN GOSUB 8228
8227 NEXT:AN=11:Z0=9:GOTO 8006
8228 IF ADVICE>=45 THEN P=FNR(ADVICE):L=60:B$="advice":GOSUB 25150:B$="Remember what I told you;"+B$:GOSUB 8232:RETURN
8230 ADVICE=ADVICE+1:P=ADVICE:L=60:B$="advice":GOSUB 25150:B$="Listen kiddo...;"+B$:GOSUB 8232:RETURN
8231 REM one word at a time/ semicolon separates phrazes
8232 P=8:LOCATE 1,9:FOR XX= 1 TO LEN(B$):A$=MID$(B$,XX,1):IF A$=";" THEN A$="":FOR TIMER= 1 TO 2300:NEXT:LOCATE 1,6:PRINT"                              ";:FOR TIMER= 1 TO 150:NEXT:LOCATE 1,9
8233 PRINT A$;:FOR Z=1 TO 75:NEXT:NEXT:FOR Q=1 TO 1100:NEXT:LOCATE 1,9:PRINT"                               ";:RETURN
8234 GOSUB 8910:AX=143:AY=96:GOSUB 8900:LOCATE LROW,LCOL:PRINT"Hit RETURN to see Control Panel  ";:RETURN
8235 REM                                                                                     fly
8236 Z0=10:GOTO 2000
8240 GOSUB 8910:AX=181:AY=96:GOSUB 8900:LOCATE LROW,LCOL:PRINT"Hit RETURN to drive the B-liner  ";:RETURN
8245 REM                                                                                     drive
8246 IF ALT>0 THEN LOCATE 1,10:PRINT"too high to drive":GOTO 8100
8248 XCOORD=INT(XCOORD):YCOORD=INT(YCOORD)
8249 CLS:LOCATE 12,8:PRINT"Bird's-eye view of the B-liner":LOCATE 14,14:PRINT"on the ground":BRANCH=1:GOSUB 25550:RUN"ground.bas
8250 GOSUB 8910:AX=260:AY=101:GOSUB 8900:LOCATE LROW,LCOL:PRINT"Hit RETURN to leave the B-liner ";:RETURN
8255 REM                                                                                     leave
8256 IF ALT>0 THEN LOCATE 1,10:PRINT"too high to leave":GOTO 8100
8260 XCOORD=INT(XCOORD):YCOORD=INT(YCOORD)
8270 HUT=0:IF STATE>144 THEN 8300
8280 FOR Z=1 TO 5:IF ((STATE*HUT%(Z))MOD 350)+(STATE MOD 15)*350=XCOORD AND ((STATE*HUT%(Z+5))MOD 350)+(STATE\15)*350=YCOORD THEN HUT=Z
8290 NEXT
8300 TYP=0:IF (XCOORD*4\7+YCOORD*3) MOD 11<>1 THEN 8320
8310 GOSUB 8540:IF CX>X-15 AND CX<X+15 AND CY>Y-15 AND CY<Y+15 THEN TYP=5
8320 IF (XCOORD*3+YCOORD*5\7) MOD 8<>1 THEN 8340
8330 GOSUB 8390:IF CX>X-15 AND CX<X+25 AND CY>Y-20 AND CY<Y+15 THEN TYP=3
8340 IF (XCOORD*2+YCOORD*3) MOD 7<>2 THEN 8360
8350 GOSUB 8440:IF CX>X-15 AND CX<X+20 AND CY>Y-35 AND CY<Y+10 THEN TYP=2
8360 IF HUT>0 THEN Z=HUT:GOSUB 8490:IF CX>X-25 AND CX<X+50 AND CY>Y-10 AND CY<Y+40 THEN TYP=4
8370 IF XCOORD=X0 AND YCOORD=Y0 AND CX>40 AND CX<90 AND CY>20 AND CY<70 THEN TYP=1
8380 GOTO 8800
8390 Q=ABS(231*XCOORD+317*YCOORD):QQ=ABS(397*XCOORD+113*YCOORD)
8400 IF Q>19600 THEN Q=Q-19600:GOTO 8400
8410 IF QQ>32400 THEN QQ=QQ-32400:GOTO 8410
8420 X=(INT(SQR(Q))*2*37) MOD 280+20:Y=(INT(SQR(QQ))*91) MOD 180+10
8430 RETURN
8440 Q=ABS(191*XCOORD+211*YCOORD):QQ=ABS(413*XCOORD+97*YCOORD)
8450 IF Q>19600 THEN Q=Q-19600:GOTO 8450
8460 IF QQ>32400 THEN QQ=QQ-32400:GOTO 8460
8470 X=(INT(SQR(Q))*2*47) MOD 280+20:Y=(INT(SQR(QQ))*87) MOD 170+20
8480 RETURN
8490 Q=HUT%(Z)*XCOORD+HUT%(Z)*YCOORD:QQ=HUT%(Z+5)*XCOORD+HUT%(Z+5)*YCOORD
8500 IF Q>19600 THEN Q=Q-19600:GOTO 8500
8510 IF QQ>32400 THEN QQ=QQ-32400:GOTO 8510
8520 X=(INT(SQR(Q))*2*67) MOD 280+20:Y=(INT(SQR(QQ))*21) MOD 170+10
8530 RETURN
8540 Q=ABS(177*XCOORD+103*YCOORD):QQ=ABS(253*XCOORD+137*YCOORD)
8550 IF Q>19600 THEN Q=Q-19600:GOTO 8550
8560 IF QQ>32400 THEN QQ=QQ-32400:GOTO 8560
8570 X=(INT(SQR(Q))*2*67) MOD 280+20:Y=(INT(SQR(QQ))*21) MOD 180+10
8580 RETURN
8800 CLS:LOCATE 10,12:PRINT"Stepping outside...":BRANCH=2:XC=238:YC=150:GOSUB 25550:RUN"ground.bas
8899 REM                                                                                    draw arrow
8900 DRAW"C3BM=ax;,=ay;d6L3F4E4L3U6L2":PAINT(AX+1,AY+4),3,3:RETURN
8909 REM                                                                                     erase arrow
8910 LINE(AX-3,AY)-(AX+5,AY+10),0,BF:RETURN
20030 REM                                                                            extract values from string                                                 .
20040 REM
20060 DEF SEG=&H40:POKE &H17,&H40:DEF SEG:REM caps lock, no num lock
20120 WIND$(0)="south":WIND$(1)="west":WIND$(2)="north":WIND$(3)="east"
20130 ALTIM=(EQUIP\2)MOD 2:HOMER=(EQUIP\4)MOD 2:FOREC=(EQUIP\8)MOD 2:FUELM=(EQUIP\16)MOD 2:PHANT=(EQUIP\32)MOD 2:RADAR=(EQUIP\64)MOD 2:RANGE=(EQUIP\128)MOD 2
20200 WEIGHT=250:MAP=0:Z0=9:X0=2995:Y0=2745:TI=FNR(4800)+200:SCREEN 1,0
20210 Z8=0:GOSUB 1000
22000 AN=10\(SLEEP\200+1)+1
24999 RETURN
25000 REM
25140 REM                                                                             get a random file                                                          .
25150 OPEN B$ AS 1 LEN=L:FIELD #1,L AS R$:GET #1,P:CLOSE:B$=R$:FOR Q=L TO 1 STEP -1:IF MID$(B$,Q,1)=" " THEN NEXT
25160 B$=LEFT$(B$,Q):RETURN
25250 REM                                                                            decrunch
25260 DEF SEG=&H4FFA:CLS:INTVAR=0:CALL DECRUNCH:RETURN
25270 REM                                                                            bload a crunched disk file
25280 PIC$=B$:DEF SEG=&H4DF1:BLOAD B$,&H80:RETURN
25449 REM                                                                            save game
25450 OPEN "savegame."+CHR$(ASC("1")+((SOFT\64)MOD 2)) FOR OUTPUT AS #2
25460 WRITE#2,HUNGRY;SLEEP;FUEL;CH(1);CH(2);CH(3);XCOORD;YCOORD;EQUIP;SOFT;CX;CY;XC;YC;WEIGHT;ITEM;ADVICE;BRANCH;TYP;ALT;BURNER;HIGH;VEL;VENT;VOLUME;GIFT;TRADE;CLUECULT;CLUECHIPS;CLUECOLOR;FLAGS
25470 CLOSE #2:RETURN
25499 REM                                                                            get common variables
25500 DEF SEG=&H4DEE:GIFT=PEEK(0):SLEEP=PEEK(1)+256*PEEK(2):FUEL=PEEK(3):CH(1)=PEEK(4)+256*PEEK(5):CH(2)=PEEK(6)+256*PEEK(7):CH(3)=PEEK(8)+256*PEEK(9):XCOORD=PEEK(10)+256*PEEK(11):YCOORD=PEEK(12)+256*PEEK(13):CX=PEEK(14)+256*PEEK(15):CY=PEEK(16)
25510 ITEM=PEEK(17):ADVICE=PEEK(18):BRANCH=PEEK(19):TYP=PEEK(20):ALT=PEEK(21)+256*PEEK(22):BURNER=PEEK(23):HIGH=PEEK(24):VEL=PEEK(25):VENT=PEEK(26):VOLUME=PEEK(27):TRADE=PEEK(28)+256*PEEK(29):EQUIP=PEEK(30):SOFT=PEEK(31)
25520 HUNGRY=PEEK(32)+256*PEEK(33):CLUECULT=PEEK(34):CLUECHIPS=PEEK(35)+256*PEEK(36):CLUECOLOR=PEEK(37):FLAGS=PEEK(38):YC=PEEK(39):XC=PEEK(40)+256*PEEK(41)
25530 DEF SEG:RETURN
25549 REM                                                                            save common variables
25550 DEF SEG=&H4DEE:POKE 0,GIFT:POKE 1,SLEEP MOD 256:POKE 2,SLEEP\256:POKE 3,FUEL:POKE 4,CH(1) MOD 256:POKE 5,CH(1)\256:POKE 6,CH(2) MOD 256:POKE 7,CH(2)\256:POKE 8,CH(3) MOD 256:POKE 9,CH(3)\256:POKE 10,XCOORD MOD 256:POKE 11,XCOORD\256
25560 POKE 12,YCOORD MOD 256:POKE 13,YCOORD\256:POKE 14,CX MOD 256:POKE 15,CX\256:POKE 16,CY:POKE 17,ITEM:POKE 18,ADVICE:POKE 19,BRANCH:POKE 20,TYP:POKE 21,ALT MOD 256:POKE 22,ALT\256:POKE 23,BURNER:POKE 24,HIGH:POKE 25,VEL
25570 POKE 26,VENT:POKE 27,VOLUME:POKE 28,TRADE MOD 256:POKE 29,TRADE\256:POKE 30,EQUIP:POKE 31,SOFT:POKE 32,HUNGRY MOD 256:POKE 33,HUNGRY\256:POKE 34,CLUECULT:POKE 35,CLUECHIPS MOD 256:POKE 36,CLUECHIPS\256:POKE 37,CLUECOLOR:POKE 38,FLAGS
25580 POKE 39,YC:POKE 40,XC MOD 256:POKE 41,XC\256
25590 DEF SEG:RETURN
