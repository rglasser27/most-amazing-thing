0REM trade
1 DIM CH(3):GOSUB 25500:KEY(1) ON:ON KEY(1) GOSUB 25450
2 DEF FNR(X)=INT(RND(1)*X+1)
3 DIM SPOT(25),SONG%(102,1),FR(3),T(5),K(5)
4 DIM EQUIP$(28),CUL$(25)
5 DIM TABCH(3),EXCHG(6,3),CLRS$(3)
6 RANDOMIZE VAL(RIGHT$(TIME$,2))
8 CULT=((YCOORD\350)-5)*5+(XCOORD\350)-5+1
9 GOSUB 20040:GOTO 1000
10 REM                                                                                 keyboard scan
11 A$=INKEY$:DEF SEG=&H40:POKE &H17,&H60:DEF SEG:IF A$="" THEN RETURN
12 IF A$="R" THEN GOSUB 100:RETURN
13 IF A$="Y" THEN GOSUB 120:RETURN
14 IF A$="G" THEN GOSUB 140:RETURN
15 LOCATE 20,10
16 IF A$="F" THEN PRINT CHR$(34);"I am a friend.";CHR$(34):GOTO 23
17 IF A$="A" THEN LOCATE 19,10:PRINT CHR$(34);"I seek the":LOCATE 21,12:PRINT"Most Amazing Thing.";CHR$(34):GOTO 23
18 IF A$="M" THEN PRINT CHR$(34);"Here is a Musix.";CHR$(34):GOTO 23
19 IF A$="C" THEN PRINT CHR$(34);"May I buy a clue?";CHR$(34):GOTO 23
20 IF A$="T" THEN LOCATE 19,11:PRINT CHR$(34);"Would you like":LOCATE 21,13:PRINT"to trade chips?";CHR$(34):GOTO 23
21 IF A$="L" THEN 2500
22 RETURN
23 P=19:GOSUB 25070:FOR Y=140 TO 170:LINE(70,Y)-STEP(178,0),0:NEXT:RETURN
25 DEF SEG=0: POKE 1050,PEEK(1052):RETURN
100 IF CH(3)-TABCH(3)<=0 THEN 110
101 TABCH(3)=TABCH(3)+1:SOUND 1000,1:IF TABCH(3)>50 THEN LOCATE 17,6:PRINT TABCH(3)"RED":GOTO 110
102 LINE(70+((TABCH(3)-1)\10)*8,120-((TABCH(3)-1)MOD 10)*3)-STEP(5,0)
110 RETURN
120 IF CH(2)-TABCH(2)<=0 THEN 130
121 TABCH(2)=TABCH(2)+1:SOUND 1000,1:IF TABCH(2)>50 THEN LOCATE 17,14:PRINT TABCH(2)"YELLOW  ":GOTO 130
122 LINE(133+((TABCH(2)-1)\10)*8,120-((TABCH(2)-1)MOD 10)*3)-STEP(5,0)
130 RETURN
140 IF CH(1)-TABCH(1)<=0 THEN 150
141 TABCH(1)=TABCH(1)+1:SOUND 1000,1:IF TABCH(1)>50 THEN LOCATE 17,26:PRINT TABCH(1)"GREEN":GOTO 150
142 LINE(196+((TABCH(1)-1)\10)*8,120-((TABCH(1)-1)MOD 10)*3)-STEP(5,0)
150 RETURN
1000 CLS:LOCATE 10,1:PRINT"The ";CUL$(CULT);" welcome you to their hut";
1001 IF (FLAGS\32)MOD 2=1 OR (FLAGS\64)MOD 2=0 OR (SOFT\128)MOD 2=0 OR CULT<>CLUECULT THEN 1004
1002 PRINT",":LOCATE 12,1:IF (FLAGS\16)MOD 2=1 THEN PRINT "and thank you again for the ";EQUIP$(GIFT);".":GOTO 1005
1003 FLAGS=FLAGS OR 16:PRINT"and accept your ";EQUIP$(GIFT);" as a gift.":GOTO 1005
1004 PRINT"-->"
1005 LOCATE 20,6:PRINT"Your chips:":LOCATE 23,5:PRINT CH(3)"RED,"CH(2)"YELLOW,"CH(1)"GREEN"
1010 P=60:GOSUB 25070
1050 B$="TABLE.CRN":GOSUB 25280:GOSUB 25260
1060 GOSUB 1930
1100 GREET=FNR(2)
1101 IF TRADE<>XCOORD THEN 1120
1102 GOSUB 25:HS=6:GOSUB 1800
1103 GOSUB 11:IF A$="" THEN 1103
1104 IF A$<>"M" THEN 1112
1105 GOSUB 3000:IF SUCCESS=1 THEN TRADE=0:GOTO 1120
1106 GOTO 2100
1112 GOSUB 25:GOSUB 1900:COUNT=0
1113 GOSUB 11:IF A$<>"M" THEN 1116
1114 GOSUB 3000:IF SUCCESS=1 THEN TRADE=0:GOTO 1120
1115 GOTO 2100
1116 COUNT=COUNT+1:IF COUNT=1000 THEN 2200
1117 GOTO 1113
1120 HS=GREET:GOSUB 1800
1200 GOSUB 11:IF A$="" THEN 1200
1210 IF A$="F" AND GREET=1 THEN 1300
1220 IF A$="A" AND GREET=2 THEN 1300
1225 IF A$="M" THEN GOSUB 3000:GOSUB 1900:GOTO 1120
1230 GOSUB 1900:GOSUB 25:COUNT=0
1240 GOSUB 11
1250 IF A$<>"M" THEN 1260
1252 GOSUB 3000:IF SUCCESS=1 THEN 1300
1254 GOTO 2100
1260 COUNT=COUNT+1:IF COUNT=1000 THEN 2000
1270 GOTO 1240
1300 GOSUB 1900:HS=3:GOSUB 1800:GOSUB 25
1310 GOSUB 11:IF A$="" THEN 1310
1320 IF A$="C" THEN 1400
1330 IF A$="T" THEN 1600
1340 IF A$="M" THEN GOSUB 3000
1350 IF A$="A" OR A$="F" THEN GOTO 1300
1360 GOTO 1310
1400 IF CLUECULT=0 THEN CLUECULT=CULT
1405 IF (FLAGS\128) MOD 2=0 THEN GOSUB 5000
1410 GOSUB 1900:GOSUB 1910:GOSUB 1920:HS=5:GOSUB 1800:P=5:GOSUB 25070
1420 LOCATE 9,18:PRINT CLRS$(CLUECOLOR)
1430 FOR X=0 TO CLUECHIPS-1:IF X>=70 THEN LOCATE 8,19:PRINT X+1:GOTO 1434
1432 LINE(130+(X\10)*8,50-(X MOD 10)*3)-STEP(5,0)
1434 SOUND 300,1:NEXT
1440 GOSUB 11:X=CULT MOD 7
1441 IF TABCH(1)*EXCHG(X,1)+TABCH(2)*EXCHG(X,2)+TABCH(3)*EXCHG(X,3)>=CLUECHIPS*EXCHG(X,CLUECOLOR) THEN 1500
1442 IF A$="" THEN 1440
1450 IF A$="C" THEN GOSUB 1910:GOTO 1400
1460 IF A$="T" THEN GOSUB 1910:GOTO 1600
1470 IF A$="M" THEN GOSUB 3000:GOTO 1300
1480 GOTO 1440
1500 GOSUB 1900:HS=3:GOSUB 1800:P=10:GOSUB 25070:GOSUB 1900:HS=6:GOSUB 1800
1520 GOSUB 25
1530 GOSUB 11:IF A$="" THEN 1530
1540 IF A$="M" THEN GOSUB 3000:GOTO 1590
1550 COUNT=0:GOSUB 1900
1560 GOSUB 11:IF A$="M" THEN GOSUB 3000:GOTO 1590
1570 COUNT=COUNT+1:IF COUNT=1000 THEN GOTO 2300
1580 GOTO 1560
1590 IF SUCCESS=1 THEN 1960
1595 GOTO 2100
1600 GOSUB 1910:GOSUB 1920
1610 IF GREET=1 THEN GREET=2:GOTO 1630
1620 GREET=1
1630 IF FNR(2)=1 THEN GOTO 1700
1640 GOSUB 1900:HS=GREET:GOSUB 1800:GOSUB 25
1650 GOSUB 11:IF A$="" THEN 1650
1660 IF A$="F" AND GREET=1 THEN 1700
1670 IF A$="A" AND GREET=2 THEN 1700
1675 IF A$="M" THEN GOSUB 3000:GOTO 1640
1680 GOSUB 1900:GOSUB 25:COUNT=0
1690 GOSUB 11:IF A$<>"M" THEN 1695
1691 GOSUB 3000:IF SUCCESS=1 THEN 1700
1692 GOTO 2100
1695 COUNT=COUNT+1:IF COUNT=1000 THEN 2000
1696 GOTO 1690
1700 GOSUB 1900:HS=4:GOSUB 1800
1710 A$=INKEY$:IF A$="" THEN 1710
1720 IF A$="R" THEN TRADECHIP=3:GOTO 1746
1730 IF A$="Y" THEN TRADECHIP=2:GOTO 1746
1740 IF A$="G" THEN TRADECHIP=1:GOTO 1746
1741 IF A$="M" THEN GOSUB 3000:GOTO 1700
1745 GOSUB 15:GOTO 1680
1746 GOSUB 1940:GOSUB 1900:HS=3:GOSUB 1800
1750 GOSUB 11:IF A$="" THEN 1750
1760 IF A$=" " THEN SOUND 200,3:GOTO 1780
1770 GOTO 1750
1780 X=CULT MOD 7:CH(1)=CH(1)-TABCH(1):CH(2)=CH(2)-TABCH(2):CH(3)=CH(3)-TABCH(3):TRADENUM=(TABCH(1)*EXCHG(X,1)+TABCH(2)*EXCHG(X,2)+TABCH(3)*EXCHG(X,3))\EXCHG(X,TRADECHIP):GOSUB 1920:GOSUB 1900:HS=5:GOSUB 1800
1790 LOCATE 9,18:PRINT CLRS$(TRADECHIP)
1793 FOR X=0 TO TRADENUM-1:IF X>=70 THEN LOCATE 8,19:PRINT X+1:GOTO 1795
1794 LINE(130+(X\10)*8,50-(X MOD 10)*3)-STEP(5,0)
1795 SOUND 300,1:NEXT
1796 CH(TRADECHIP)=CH(TRADECHIP)+TRADENUM
1797 P=60:GOSUB 25070:CLS:LOCATE 10,16:PRINT"Your chips:":LOCATE 13,5:PRINT CH(3)"RED,"CH(2)"YELLOW,"CH(1)"GREEN":P=60:GOSUB 25070:GOSUB 25260
1798 GOSUB 1922:GOTO 1300
1800 REM                                                                                hand signs                                                              .
1805 FOR X=1 TO 1000 STEP 100:SOUND 10000+X,1:NEXT
1810 DIR$="lrefghu"
1820 REM                                                                                     translate
1830 GOSUB 1850:PSET(FNR(30)+130,18),0:DRAW"c3u10n"+T1$+"5bm+7,+10u10"+T2$+"5"
1835 GOSUB 25
1840 REM                                                                                    compute tentacle shapes
1850 T1$=MID$(DIR$,((CULT\4 + (HS-1)) MOD 7)+1,1):T2$=MID$(DIR$,((CULT+(HS-1)) MOD 7)+1,1):RETURN
1900 FOR Y=0 TO 18:LINE(124,Y)-(176,Y),0:NEXT:RETURN:REM CLEAR TRADER SIGNAL
1910 FOR Y=20 TO 71:LINE(130,Y)-(185,Y),0:NEXT:RETURN:REM CLEAR TRADER'S CHIPS
1920 FOR Y=90 TO 120:LINE(70,Y)-(250,Y),0:NEXT:IF TABCH(1)>999 OR TABCH(2)>999 OR TABCH(3)>999 THEN GOSUB 25260
1922 GOSUB 1930:TABCH(1)=0:TABCH(2)=0:TABCH(3)=0:RETURN:REM CLEAR PLAYER CHIPS
1930 LOCATE 17,6:PRINT " R=RED     Y=YELLOW   G=GREEN ";:LOCATE 23,1:PRINT"F=Friend    T=Trade?    A=Amazing Thing";:LOCATE 25,1:PRINT"M=Musix         L=Leave        C=Clue?";:RETURN
1940 LOCATE 23,1:PRINT" (Pile your chips on the table, then   ";:LOCATE 25,1:PRINT" press the spacebar to offer them.)   ";:RETURN
1959 REM  clues
1960 FOR X=1 TO 200:SOUND 900,2:SOUND 500,0:NEXT
1961 CH(1)=CH(1)-TABCH(1):CH(2)=CH(2)-TABCH(2):CH(3)=CH(3)-TABCH(3):GOSUB 1920:GOSUB 1900:GOSUB 1910:GOSUB 25240:CLS:LOCATE 1,1:PRINT"CLUE:":LOCATE 25,1:PRINT"(Press any key to continue)";
1962 IF CLUECULT <> CULT THEN LOCATE 10,10:PRINT"Go to "CUL$(CLUECULT)".":LOCATE 13,10:PRINT"They know more than we do.";:GOTO 1990
1970 IF (FLAGS\32) MOD 2=1 THEN LOCATE 10,10:PRINT"Lure a Mire Crab onto":LOCATE 12,10:PRINT"a bunchmark near here.":GOTO 1990
1971 IF (FLAGS\64) MOD 2=1 AND (SOFT\128) MOD 2=0 THEN LOCATE 10,10:PRINT"If you had brought one of":LOCATE 12,10:PRINT"those "EQUIP$(GIFT)" gadgets with you,":LOCATE 14,10:PRINT"we could have told you more.":GOTO 1990
1972 CLUECULT=FNR(25):IF CLUECULT=CULT THEN 1972
1973 IF (FLAGS\64) MOD 2=1 THEN FLAGS=FLAGS OR &H20:LOCATE 10,10:PRINT"Lure a Mire Crab onto any":LOCATE 12,10:PRINT"bunchmark near the middle of ":LOCATE 14,10:PRINT CUL$(CLUECULT):FLAGS=FLAGS AND &H7F:GOTO 1990
1980 FLAGS=FLAGS OR &H40:LOCATE 10,10:PRINT"Take one "EQUIP$(GIFT)" to the "CUL$(CLUECULT)".":LOCATE 12,10:PRINT"They will be able to help.":FLAGS=FLAGS AND &H7F
1990 GOSUB 25
1992 A$=INKEY$:IF A$="" THEN 1992
1995 GOSUB 25260:GOSUB 1910:GOTO 1300
2000 CLS:LOCATE 10,10:PRINT"The head trader did not like":LOCATE 12,10:PRINT"your response to his question.":TRADE=XCOORD:GOTO 2500
2100 CLS:LOCATE 10,10:PRINT"The head trader did not like":LOCATE 12,10:PRINT"your song.":TRADE=XCOORD:GOTO 2500
2200 CLS:LOCATE 10,10:PRINT"The head trader got tired of":LOCATE 12,10:PRINT"waiting for a musix.":TRADE=XCOORD:GOTO 2500
2300 CLS:LOCATE 10,10:PRINT"The head trader does not give":LOCATE 12,10:PRINT"clues without first hearing":LOCATE 13,10:PRINT"a musix.":TRADE=XCOORD:GOTO 2500
2500 BRANCH=2:GOSUB 25550:RUN"ground.bas"
3000 X=0:Y=1
3010 SCREEN 1:CLS:LINE(40,15)-(280,151),3,B:LINE(35,9)-(285,157),3,B:PAINT(50,156),2,3:LINE(39,14)-(281,152),0,B
3015 PAINT(50,149),1,3
3020 LOCATE 24,3:PRINT"My latest Musix for the Head Trader";:P=15:GOSUB 25070
3030 OPEN "song."+CHR$(ASC("1")+((SOFT\64)MOD 2)) FOR INPUT AS #1:INPUT #1,RATING,CUR:FOR Z1=0 TO CUR:FOR Z2=X TO Y:INPUT #1,SONG%(Z1,Z2):NEXT:NEXT:CLOSE #1
3040 PSET(SONG%(0,X),SONG%(0,Y)),0:FOR Z1=1 TO CUR-1:LINE -(SONG%(Z1,X),SONG%(Z1,Y)),0:SOUND 2000-5*(SONG%(Z1,Y)),3:NEXT
3050 P=20:GOSUB 25070
3060 GOSUB 25260:GOSUB 1930
3065 SUCCESS=0
3070 IF CULT MOD 2=1 AND CUR>5 AND CUR<40 THEN SUCCESS=1
3080 IF CULT MOD 2=0 AND CUR>30 THEN SUCCESS=1
3090 CUR=1:RATING=1:OPEN "song."+CHR$(ASC("1")+((SOFT\64)MOD 2)) FOR OUTPUT AS #1:WRITE #1,RATING,CUR:FOR Z1=0 TO CUR:FOR Z2=X TO Y:WRITE #1,SONG%(Z1,Z2):NEXT:NEXT:CLOSE #1:GOSUB 25:RETURN
5000 X=CLUECULT MOD 7
5010 IF X=4 OR X=6 THEN CLUECOLOR=3
5020 IF X=3 OR X=5 THEN CLUECOLOR=2
5030 IF X=1 OR X=2 THEN CLUECOLOR=1
5040 IF X=0 THEN CLUECOLOR=FNR(3)
5050 CLUECHIPS=CH(1)*EXCHG(X,1)+CH(2)*EXCHG(X,2)+CH(3)*EXCHG(X,3)
5060 IF CLUECHIPS>32767 THEN CLUECHIPS=8192+FNR(10):GOTO 5080
5070 CLUECHIPS=CLUECHIPS\4+FNR(10)+1
5080 FLAGS=FLAGS OR 128:RETURN
20040 REM
20100 DEF SEG=&H40:POKE &H17,&H60:DEF SEG:REM caps lock and num lock
20230 FOR X=0 TO 28:READ EQUIP$(X):NEXT
20232 DATA "dIR-?","High?","HOME1","Storm","Fuelo","AutoP","Radar","Hut 1","Posix","Map-H","Cults","Dicto","Musix","Trac4","Hovak","Essex","LIFT5","Windq","XXX-2","Gasex","Namex","Turbo","AbCdE","Takom","Zener","Fazer","Yukal","Plaxi","Namek"
20240 FOR X=1 TO 25:READ CUL$(X):NEXT
20241 DATA "Camawhy","Logretch","Watersi","Beribey","Paelazia","Fondef","Kartoff","Zyzy","Girfleez","Jave","Muffiji","Undeflas","Odask","Torbion","Sloping","Nutrile","Hogaan","Forthel","Laska","Treliss","Fastak","Thomizek","Muhill","Maxaze","Solvaque"
20250 FOR X=0 TO 6:FOR Y=1 TO 3:READ EXCHG(X,Y):NEXT:NEXT
20251 DATA 4,4,4,4,2,1,4,1,2,2,4,1,2,1,4,1,4,2,1,2,4
20260 FOR X=1 TO 3:READ CLRS$(X):NEXT
20261 DATA "GREEN","YELLOW","RED"
24999 RETURN
25000 REM
25060 REM                                                                             timer                                                                      .
25070 FOR Q=1 TO P*100:NEXT:RETURN
25080 REM
25140 REM                                                                             get a random file                                                          .
25150 OPEN B$ AS 1 LEN=L:FIELD #1,L AS R$:GET #1,P:CLOSE:B$=R$:FOR Q=L TO 1 STEP -1:IF MID$(B$,Q,1)=" " THEN NEXT
25160 B$=LEFT$(B$,Q):RETURN
25230 REM                                                                            crunch
25240 DEF SEG=&H4DF1:CALL CRUNCH:RETURN
25250 REM                                                                            decrunch
25260 DEF SEG=&H4FFA:CLS:CALL DECRUNCH:RETURN
25270 REM                                                                            bload a crunched disk file
25280 PIC$=B$:DEF SEG=&H4DF1:BLOAD B$,&H80:RETURN
25310 REM                                                                                                                                                            line clearer
25320 FOR Q=P TO L:LOCATE Q,1:PRINT STRING$(39," ");:NEXT:RETURN
25449 REM                                                                            save game
25450 OPEN "savegame."+CHR$(ASC("1")+((SOFT\64)MOD 2)) FOR OUTPUT AS #2
25460 WRITE#2,HUNGRY;SLEEP;FUEL;CH(1);CH(2);CH(3);XCOORD;YCOORD;EQUIP;SOFT;CX;CY;XC;YC;WEIGHT;ITEM;ADVICE;BRANCH;TYP;ALT;BURNER;HIGH;VEL;VENT;VOLUME;GIFT;TRADE;CLUECULT;CLUECHIPS;CLUECOLOR;FLAGS
25470 CLOSE #2:RETURN
25499 REM                                                                            get common variables
25500 DEF SEG=&H4DEE:GIFT=PEEK(0):SLEEP=PEEK(1)+256*PEEK(2):FUEL=PEEK(3):CH(1)=PEEK(4)+256*PEEK(5):CH(2)=PEEK(6)+256*PEEK(7):CH(3)=PEEK(8)+256*PEEK(9):XCOORD=PEEK(10)+256*PEEK(11):YCOORD=PEEK(12)+256*PEEK(13):CX=PEEK(14)+256*PEEK(15):CY=PEEK(16)
25510 ITEM=PEEK(17):ADVICE=PEEK(18):BRANCH=PEEK(19):TYP=PEEK(20):ALT=PEEK(21)+256*PEEK(22):BURNER=PEEK(23):HIGH=PEEK(24):VEL=PEEK(25):VENT=PEEK(26):VOLUME=PEEK(27):TRADE=PEEK(28)+256*PEEK(29):EQUIP=PEEK(30):SOFT=PEEK(31)
25520 HUNGRY=PEEK(32)+256*PEEK(33):CLUECULT=PEEK(34):CLUECHIPS=PEEK(35)+256*PEEK(36):CLUECOLOR=PEEK(37):FLAGS=PEEK(38):YC=PEEK(39):XC=PEEK(40)+256*PEEK(41)
25530 DEF SEG:RETURN
25549 REM                                                                            save common variables
25550 DEF SEG=&H4DEE:POKE 0,GIFT:POKE 1,SLEEP MOD 256:POKE 2,SLEEP\256:POKE 3,FUEL:POKE 4,CH(1) MOD 256:POKE 5,CH(1)\256:POKE 6,CH(2) MOD 256:POKE 7,CH(2)\256:POKE 8,CH(3) MOD 256:POKE 9,CH(3)\256:POKE 10,XCOORD MOD 256:POKE 11,XCOORD\256
25560 POKE 12,YCOORD MOD 256:POKE 13,YCOORD\256:POKE 14,CX MOD 256:POKE 15,CX\256:POKE 16,CY:POKE 17,ITEM:POKE 18,ADVICE:POKE 19,BRANCH:POKE 20,TYP:POKE 21,ALT MOD 256:POKE 22,ALT\256:POKE 23,BURNER:POKE 24,HIGH:POKE 25,VEL
25570 POKE 26,VENT:POKE 27,VOLUME:POKE 28,TRADE MOD 256:POKE 29,TRADE\256:POKE 30,EQUIP:POKE 31,SOFT:POKE 32,HUNGRY MOD 256:POKE 33,HUNGRY\256:POKE 34,CLUECULT:POKE 35,CLUECHIPS MOD 256:POKE 36,CLUECHIPS\256:POKE 37,CLUECOLOR:POKE 38,FLAGS
25580 POKE 39,YC:POKE 40,XC MOD 256:POKE 41,XC\256
25590 DEF SEG:RETURN
